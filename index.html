<!DOCTYPE html>
<html lang="en">

<head>
	<title>2001 Portal</title>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<link type="text/css" rel="stylesheet" href="main.css">
</head>

<body>

	<!-- Hello Recurse Center! For my 'program from scratch', I would like for you to focus on the work
			contained within the vertex and fragment shaders sections below. This is the first shader program
			I've attempted to write rather than simply adapt!
			
			My inspiration for this shader was the 'star gate' sequence of 2001 Space Odyssey. The longer it runs,
			the 'deeper' we travel into the vortex. I set for myself the goal of creating this one-page app in one
			work day, and to add only the most minimal Three.js scaffolding to showcase the visualization.

			"From scratch" in this context comes with a little bit of help from the great Ken Perlin's essential simplex noise algorithm.
			Also, the strategies and implementations of these visuals were inspired by great work from others.

			Some of those inspirations include Inigo Quilez's post about tunnels: http://www.iquilezles.org/www/articles/tunnel/tunnel.htm
			I also read through a number of star field implementations in Shadertoy: https://www.shadertoy.com/results?query=star+field

			Consider this a snapshot in time of my ongoing self-driven study around computer graphics programming
			for the web.
		-->

	<div id="container"></div>
	<div id="info"></div>

	<script src="./three.js"></script>

	<script src="./WebGL.js"></script>

	<script id="vertexShader" type="x-shader/x-vertex">

			uniform float u_time;
			uniform float u_duration;
			varying vec2 vUv;

			// Since I've elected to treat this 'program from scratch' scenario as a one-day spike, I include here
			// a breadcrumb trail to various (non-trivial) strategies towards re-use of custom shader code here:
			// https://medium.com/@pailhead011/extending-three-js-materials-with-glsl-78ea7bbb9270
			// 
			// I am punting on this code-organizational improvement in favor of focusing on my original intent:
			// writing a visually engaging first shader.
			// 
			// So, since re-using shader code is non trivial and I want to use simplex noise on both the vertex shader's
			// pixel positions as well as various elements of the fragment shader, I will use two different implementations
			// I found, for variety and fun.
			//
			// (Also, note: These are the elements of my shader program that are NOT written from 'scratch', but
			// rather by Ken Perlin, in 2001!)
			//
			// This simplex noise implementation was written by Inigo Quilez:
			vec2 hash( vec2 p )
			{
				p = vec2( dot(p,vec2(127.1,311.7)), dot(p,vec2(269.5,183.3)) );
				return -1.0 + 2.0*fract(sin(p)*43758.5453123);
			}
			
			float noise( in vec2 p )
			{
				const float K1 = 0.366025404; // (sqrt(3)-1)/2;
				const float K2 = 0.211324865; // (3-sqrt(3))/6;
				vec2  i = floor( p + (p.x+p.y)*K1 );
				vec2  a = p - i + (i.x+i.y)*K2;
				float m = step(a.y,a.x); 
				vec2  o = vec2(m,1.0-m);
				vec2  b = a - o + K2;
				vec2  c = a - 1.0 + 2.0*K2;
				vec3  h = max( 0.5-vec3(dot(a,a), dot(b,b), dot(c,c) ), 0.0 );
				vec3  n = h*h*h*h*vec3( dot(a,hash(i+0.0)), dot(b,hash(i+o)), dot(c,hash(i+1.0)));
				return dot( n, vec3(70.0) );
			}

			void main()	{
				// Here we offset the vertex shader pixels with noise to bend the portal at fun angles.
				vUv = uv + noise(uv * u_time/u_duration);
				gl_Position = vec4( position, 1.0 );
			}

		</script>

	<script id="fragmentShader" type="x-shader/x-fragment">

			uniform float u_time;
			uniform vec2 u_resolution;
			uniform float u_duration;
			varying vec2 vUv;
			#define PI 3.14159	
			
			// The simplex implementation I use here in the fragment shader is from Patricio Gonzalez Vivo's gist:
			// https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
			vec3 permute(vec3 x) { 
				return mod(((x*34.0)+1.0)*x, 289.0);
			}
			
			float noise(vec2 v){
				const vec4 C = vec4(0.211324865405187, 0.366025403784439,
						-0.577350269189626, 0.024390243902439);
				vec2 i  = floor(v + dot(v, C.yy) );
				vec2 x0 = v -   i + dot(i, C.xx);
				vec2 i1;
				i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
				vec4 x12 = x0.xyxy + C.xxzz;
				x12.xy -= i1;
				i = mod(i, 289.0);
				vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
				+ i.x + vec3(0.0, i1.x, 1.0 ));
				vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
					dot(x12.zw,x12.zw)), 0.0);
				m = m*m ;
				m = m*m ;
				vec3 x = 2.0 * fract(p * C.www) - 1.0;
				vec3 h = abs(x) - 0.5;
				vec3 ox = floor(x + 0.5);
				vec3 a0 = x - ox;
				m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
				vec3 g;
				g.x  = a0.x  * x0.x  + h.x  * x0.y;
				g.yz = a0.yz * x12.xz + h.yz * x12.yw;
				return 130.0 * dot(m, g);
			}	
			
			// Create some psychedelic patterns!
			vec3 its2001(in vec2 uv){
				// Where in the sequence are we?
				float sequence_position = fract(u_time / u_duration);
				// Sprinkle a bit of noise offset to the rotation that we'll add to the
				// position of these pixels.
				float angle_offset = .02001 * noise(uv);
				float u_angle =  pow(u_time, sequence_position * angle_offset);
				float sin_factor = sin(u_angle);
				float cos_factor = cos(u_angle);
				// add a spin/rotation sensation
				uv *= mat2(sin_factor, cos_factor, -sin_factor, cos_factor) + angle_offset;
				// Make the color palette change with the changing rotational angle.
				vec3 color_palette = vec3(fract(u_angle * .5), fract(-u_angle), fract(u_angle));
				vec3 time_offset_color_palette = fract(pow(sequence_position, .3) + color_palette);
				float pattern_size_offset = max(100., 100. * cos(2. * PI * sequence_position));
				// Here we modulate "acceleration" of the pixel's value along a sin wave.
				return sin(pattern_size_offset * uv.x / exp2(time_offset_color_palette)) + 
						 sin(pattern_size_offset * uv.y / exp2(time_offset_color_palette));  
			}
			
			void main( )
			{
				// Normalized coordinates of -1 to 1 vertically.
				vec2 pos = (-u_resolution.xy + 2.0 * vUv) / u_resolution.y;
				// Get the radius from this pixel position to the 'center' using a high-n root of 2x^2
				// so as to create a tunnel effect that cuts across the middle y of the screen
				// ...the higher the root, the more narrow the tunnel.
				float radius = pow(pow(pos.x * pos.x, 4.0) + pow(pos.x * pos.x, 4.0), 1.0 / 6.0 );
				vec2 uv;
				// Use the inverse radius as the pixel's x position and add a time offset for the 'flying' animation.
				uv.x = .3 / radius + 1.1 * u_time;
				// In order to create the 'tunnel' effect on a 2d plane, we translate the cartesian coordinates
				// to polar coordinates and invert the radius. Since uv derivatives are calculated by the gpu in
				// 2x2 blocks, they can fall imperfectly within a screen resolution and cause graphical artifacts
				// (i.e. a line across the screen). We take the absolute value of x calculating the partial deriviative
				// of y in order to quantize the position into a specific pixel
				// (Source: http://www.iquilezles.org/www/articles/tunnel/tunnel.htm)
				uv.y = atan(pos.y, abs(pos.x))/PI;
				// Add an increasingly-trippy decoration to the walls of the tunnel.
				vec3 color = its2001(uv);
				// Darken our shader at the center to give it the look of a dark portal.
				color *= radius;
				gl_FragColor= vec4( color, 1.0 );
			}

		</script>

	<script>

		if (WEBGL.isWebGLAvailable() === false) {

			document.body.appendChild(WEBGL.getWebGLErrorMessage());

		}

		var container;

		var camera, scene, renderer;

		var uniforms;

		init();
		animate();

		function init() {

			container = document.getElementById('container');

			camera = new THREE.OrthographicCamera(- 1, 1, 1, - 1, 0, 1);

			scene = new THREE.Scene();

			var geometry = new THREE.PlaneBufferGeometry(2, 2);

			let resolution = new THREE.Vector2(.5, 1);

			uniforms = {
				"u_time": { value: 0 },
				"u_resolution": { value: resolution },
				"u_duration": { value: 40. }
			};

			var material = new THREE.ShaderMaterial({

				uniforms: uniforms,
				vertexShader: document.getElementById('vertexShader').textContent,
				fragmentShader: document.getElementById('fragmentShader').textContent

			});

			var mesh = new THREE.Mesh(geometry, material);
			scene.add(mesh);

			renderer = new THREE.WebGLRenderer();
			renderer.setPixelRatio(window.devicePixelRatio);
			container.appendChild(renderer.domElement);

			onWindowResize();

			window.addEventListener('resize', onWindowResize, false);

		}

		function onWindowResize() {

			renderer.setSize(window.innerWidth, window.innerHeight);

		}

		function animate(timestamp) {

			requestAnimationFrame(animate);

			uniforms["u_time"].value = timestamp / 1000;

			renderer.render(scene, camera);

		}

	</script>

</body>

</html>